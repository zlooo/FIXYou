buildscript {
    repositories {
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "me.champeau.gradle:jmh-gradle-plugin:0.5.0"
    }
}

plugins {
    id "fr.brouillard.oss.gradle.jgitver" version "0.9.1"
    id "org.sonarqube" version "3.1.1"
    id("io.github.gradle-nexus.publish-plugin") version "1.1.0"
}

ext {
    nettyVersion = '4.1.68.Final'
    slf4jVersion = '1.7.32'
    lombokVersion = '1.18.20'
    daggerVersion = '2.39.1'
    findbugsJsr305Version = '3.0.2'
    javaxInjectVersion = '1'
    disruptorVersion = '3.4.4'
    hppcrtVersion = '0.7.5'
    //    test dependencies
    spockVersion = '2.0-groovy-3.0'
    assertJVersion = '3.14.0'
    springVersion = '5.3.10'
    byteBuddyVersion = '1.11.19'
    objenesisVersion = '3.2'
    quickfixVersion = '2.3.0'
    logbackClassicVersion = '1.2.6'
    transfixVersion = '1.0.2-alpha'
    commonsLangVersion = '3.12'
    jmhVersion = '1.25'
}

allprojects {
    group 'io.github.zlooo.fixyou'
    repositories {
        mavenCentral()
        mavenLocal()
    }
}

subprojects {
    apply plugin: 'java-library'
    apply plugin: 'groovy'
    apply plugin: 'checkstyle'
    apply plugin: 'jacoco'
    apply plugin: 'maven-publish'
    apply plugin: 'me.champeau.gradle.jmh'
    apply plugin: 'signing'

    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8

    java {
        withSourcesJar()
        withJavadocJar()
    }

    sourceSets {
        integrationTest {
            compileClasspath += sourceSets.main.output
            runtimeClasspath += sourceSets.main.output
        }
    }

    configurations {
        integrationTestImplementation.extendsFrom implementation
        integrationTestRuntimeOnly.extendsFrom runtimeOnly
    }

    dependencies {
        compileOnly group: 'com.google.code.findbugs', name: 'jsr305', version: "$findbugsJsr305Version"
        compileOnly "org.projectlombok:lombok:1.18.20"
        annotationProcessor "org.projectlombok:lombok:1.18.20"
        annotationProcessor "com.google.dagger:dagger-compiler:2.39.1"
        testImplementation platform("org.spockframework:spock-bom:$spockVersion")
        testImplementation group: 'org.spockframework', name: 'spock-core'
        testImplementation group: 'net.bytebuddy', name: 'byte-buddy', version: "$byteBuddyVersion"
        testImplementation group: 'org.objenesis', name: 'objenesis', version: "$objenesisVersion"
        testImplementation group: 'org.assertj', name: 'assertj-core', version: "$assertJVersion"
        testImplementation group: 'org.slf4j', name: 'slf4j-simple', version: "$slf4jVersion"
        integrationTestImplementation platform("org.spockframework:spock-bom:$spockVersion")
        integrationTestImplementation group: 'org.spockframework', name: 'spock-core'
        integrationTestImplementation group: 'org.assertj', name: 'assertj-core', version: "$assertJVersion"
        integrationTestImplementation group: 'org.springframework', name: 'spring-test', version: "$springVersion"
        integrationTestImplementation group: 'ch.qos.logback', name: 'logback-classic', version: "$logbackClassicVersion"
        jmhAnnotationProcessor "org.projectlombok:lombok:1.18.20"
        jmhAnnotationProcessor group: 'org.openjdk.jmh', name: 'jmh-generator-annprocess', version: "$jmhVersion"
    }

    test {
        useJUnitPlatform()
        testLogging {
            events "skipped", "failed"
        }
    }

    task integrationTest(type: Test) {
        description = 'Runs integration tests.'
        group = 'verification'

        testClassesDirs = sourceSets.integrationTest.output.classesDirs
        classpath = sourceSets.integrationTest.runtimeClasspath
        shouldRunAfter test
        useJUnitPlatform()
        testLogging {
            events "skipped", "failed"
        }
    }

    jacocoTestReport {
        executionData.from({
            if (new File("${buildDir}/jacoco/integrationTest.exec").exists()) {
                executionData("${buildDir}/jacoco/integrationTest.exec")
            }
        })
        reports {
            xml.enabled true
        }
    }

    checkstyle {
        toolVersion = '8.28'
        sourceSets = [sourceSets.main]
    }

    publishing {
        publications {
            "${project.name}"(MavenPublication) {
                from components.java
                pom {
                    name = "FIXYou - ${project.name}"
                    url = 'https://github.com/zlooo/FIXYou'
                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }
                    developers {
                        developer {
                            id = 'zlooo'
                            name = 'Krzysztof Dworakowski'
                            email = 'zlooo.inc@gmail.com'
                        }
                    }
                    scm {
                        connection = 'scm:git:https://github.com/zlooo/FIXYou.git'
                        developerConnection = 'scm:git:https://github.com/zlooo/FIXYou.git'
                        url = 'https://github.com/zlooo/FIXYou'
                    }
                }
                pom.withXml {
                    asNode().appendNode('description', "${project.description}")
                }
            }
        }
    }

    signing {
        sign publishing.publications."${project.name}"
    }
}

nexusPublishing {
    repositories {
        sonatype()
    }
}